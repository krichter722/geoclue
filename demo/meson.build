desktop_dir = get_option('datadir') + '/applications'
demo_dir = get_option('libexecdir') + '/geoclue-2.0/demos'

i18n = import('i18n')
desktop_conf = configuration_data()
desktop_conf.set('libexecdir', get_option('libexecdir'))

if get_option('enable-libgeoclue')
    include_dirs = [ configinc,
                     libgeoclue_public_api_inc,
                     include_directories('../libgeoclue') ]

    c_args = [ '-Werror' ]
    executable('where-am-i',
               'where-am-i.c',
               include_directories: include_dirs,
               c_args: c_args,
               link_with: [ libgeoclue ],
               dependencies: base_deps,
               install: true,
               install_dir: demo_dir)
    desktop_in = configure_file(output: 'geoclue-where-am-i.desktop.in',
                                input: 'geoclue-where-am-i.desktop.in.in',
                                configuration: desktop_conf)
    i18n.merge_file(output: 'geoclue-where-am-i.desktop',
                    input: desktop_in,
                    type: 'desktop',
                    po_dir: '../po',
                    install: true,
                    install_dir: desktop_dir)
endif

if get_option('enable-demo-agent')
    include_dirs = [ configinc,
                     libgeoclue_public_api_inc,
                     libgeoclue_agent_inc ]

    c_args = [ '-Werror' ]
    sources = [ 'gclue-service-agent.h',
                'gclue-service-agent.c',
                'agent.c' ]
    deps = base_deps + [ dependency('libnotify') ]
    executable('agent',
               sources,
               include_directories: include_dirs,
               c_args: c_args,
               link_with: [ libgeoclue_agent ],
               dependencies: deps,
               install: true,
               install_dir: demo_dir)

    desktop_in = configure_file(output: 'geoclue-demo-agent.desktop.in',
                                input: 'geoclue-demo-agent.desktop.in.in',
                                configuration: desktop_conf,
                                install_dir: desktop_dir)
    i18n.merge_file(output: 'geoclue-demo-agent.desktop',
                    input: desktop_in,
                    type: 'desktop',
                    po_dir: '../po',
                    install: true,
                    install_dir: desktop_dir)
endif
